cmake_minimum_required(VERSION 3.15)
project(app-stiggi-dj VERSION 0.1.0)

# Only do these if this is the main project,
# and not if it is included through add_subdirectory.
if (CMAKE_PROJECT_NAME STREQUAL PROJECT_NAME)
    # Caches build artifacts for faster builds
    find_program(CCACHE ccache)
    if (CCACHE)
        set(CMAKE_CXX_COMPILER_LAUNCHER ${CCACHE})
    endif ()

    # Set C11 with no extensions enabled. Fail if the
    # standard is not supported.
    set(CMAKE_C_STANDARD 11)
    set(CMAKE_C_STANDARD_REQUIRED ON)
    set(CMAKE_C_EXTENSIONS OFF)

    # Set C++14 with no extensions enabled. Fail if the
    # standard is not supported.
    set(CMAKE_CXX_STANDARD 14)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
    set(CMAKE_CXX_EXTENSIONS OFF)

    # library settings
    set(CMAKE_POSITION_INDEPENDENT_CODE ON)
    set(CMAKE_CXX_VISIBILITY_PRESET hidden)
    set(BUILD_SHARED_LIBS OFF)

    # misc
    set_property(GLOBAL PROPERTY USE_FOLDERS ON)
    set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

    # Minimum macOS version
    set(CMAKE_OSX_DEPLOYMENT_TARGET "10.11")
endif ()

add_subdirectory(3rd_party/JUCE)

juce_add_gui_app(StiggiDJ
    COMPANY_NAME "tobanteAudio"         # Specify the name of the app's author
    PRODUCT_NAME "Stiggi DJ"            # The name of the final executable, which can differ from the target name
)

target_sources(StiggiDJ
    PRIVATE
        "src/Main.cpp"
        "src/MainComponent.hpp"
        "src/MainComponent.cpp"

        "src/Core/Array.hpp"

        "src/DSP/AudioFileSource.cpp"
        "src/DSP/AudioFileSource.hpp"

        "src/UI/Component/JogWheel.cpp"
        "src/UI/Component/JogWheel.hpp"
        "src/UI/Component/Placeholder.cpp"
        "src/UI/Component/Placeholder.hpp"

        "src/UI/Section/MainSection.cpp"
        "src/UI/Section/MainSection.hpp"
        "src/UI/Section/SideBarLeft.cpp"
        "src/UI/Section/SideBarLeft.hpp"
        "src/UI/Section/SideBarRight.cpp"
        "src/UI/Section/SideBarRight.hpp"
)

target_compile_definitions(StiggiDJ
    PRIVATE
        JUCE_WEB_BROWSER=0  # If you remove this, add `NEEDS_WEB_BROWSER TRUE` to the `juce_add_gui_app` call
        JUCE_USE_CURL=0     # If you remove this, add `NEEDS_CURL TRUE` to the `juce_add_gui_app` call
        JUCE_APPLICATION_NAME_STRING="$<TARGET_PROPERTY:StiggiDJ,JUCE_PRODUCT_NAME>"
        JUCE_APPLICATION_VERSION_STRING="$<TARGET_PROPERTY:StiggiDJ,JUCE_VERSION>"
)

target_include_directories(StiggiDJ
    PRIVATE
        ${CMAKE_CURRENT_SOURCE_DIR}/src
)

target_link_libraries(StiggiDJ
    PRIVATE
        juce::juce_gui_extra
        juce::juce_audio_basics
        juce::juce_audio_devices
        juce::juce_audio_formats
    PUBLIC
        juce::juce_recommended_config_flags
        juce::juce_recommended_lto_flags
        juce::juce_recommended_warning_flags
)
